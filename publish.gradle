apply plugin: 'signing'
apply plugin: 'com.vanniktech.maven.publish'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from(android.sourceSets.main.java.srcDirs)
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    failOnError = false
}

task javadocsJar(type: Jar) {
    classifier = 'javadoc'
    from(androidJavadocs)
}

def siteUrl = 'https://github.com/willowtreeapps/Hyperion-Android'
def gitUrl = 'https://github.com/willowtreeapps/Hyperion-Android.git'

publishing {
    publications {
        lib(MavenPublication) {
            from components.android
            artifact sourcesJar
            artifact javadocsJar

            pom {
                name = project.name
                description = 'App Debugging & Inspection Tool for Android'
                url = siteUrl
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'kritarie'
                        name = 'Sean Amos'
                    }
                }
                scm {
                    connection = gitUrl
                    developerConnection = gitUrl
                    url = siteUrl
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username project.findProperty('sonatype.username')
                password project.findProperty('sonatype.password')
            }
        }
    }
}

signing {
    sign publishing.publications.lib
}